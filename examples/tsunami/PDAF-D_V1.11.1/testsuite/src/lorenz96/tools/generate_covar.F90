! $Id: generate_covar.F90 1165 2011-09-14 13:38:14Z lnerger $
!BOP
!
! !Program: generate_covar --- Compute covariance matrix from state trajectory
!
! !INTERFACE:
PROGRAM generate_covar

! !DESCRIPTION:
! This programm to computes a covariance matrix from a
! model trajectory of Lorenz96. The matrix is decomposed in
! EOFs and stored in form of eigenvalues and eigenvectors.
!
! The matrix is generated by a singular value decomposition
! of the perturbation matrix of a long state trajectory
! about its long time mean.

! !USES:
  IMPLICIT NONE

  INCLUDE 'netcdf.inc'
!EOP
  
! Local variables
  INTEGER :: i, k, s, iter, maxtimes, rank
  CHARACTER(len=120) :: inpath, outpath
  CHARACTER(len=120) :: infile, outfile
  CHARACTER(len=150) :: ncfile_in, ncfile_out
  CHARACTER(len=150) :: attstr
  INTEGER :: ncid_in, ncid_out
  INTEGER :: id_dim, id_time, id_state
  INTEGER :: id_mean, id_sigma, id_svec
  INTEGER :: dimid_rank, dimid_state, dimid_one
  INTEGER :: steps, dim_state
  INTEGER :: stat(100)
  INTEGER :: countv(2), startv(2)
  INTEGER :: dimids(2)
  REAL :: limit
  REAL, ALLOCATABLE :: times(:)
  REAL, ALLOCATABLE :: states(:, :)
  REAL, ALLOCATABLE :: meanstate(:)
  INTEGER :: dgesvd_info,ldwork            ! variables for SVD routine 
  REAL,ALLOCATABLE,DIMENSION(:) :: svals   ! field of singular values
  REAL,ALLOCATABLE,DIMENSION(:) :: work    ! working array for SVD
  REAL,ALLOCATABLE,DIMENSION(:,:) :: svdU  ! left singular vectors
  REAL :: svdV    ! right singular values (not referenced)


! ************************************************
! *** Configuration                            ***
! ************************************************

  ! Maximum number if time slices to consider
  maxtimes = 1000000

  ! Path to and name of file holding model trajectory
  inpath = '../../../bin/'
  infile = 'state.nc'

  ! Path to and name of output file holding covariance matrix
  outpath = '../../../bin/'
  outfile = 'covar.nc'

  ! Lower limit for eigenvalue
  limit = 1.0e-12


! ************************************************
! *** Init                                     ***
! ************************************************

  WRITE (*,'(10x,a)') '*******************************************'
  WRITE (*,'(10x,a)') '*             GENERATE_COVAR              *'
  WRITE (*,'(10x,a)') '*                                         *'
  WRITE (*,'(10x,a)') '*    Compute covariance matrix and mean   *'
  WRITE (*,'(10x,a)') '*     state from a sequence of states.    *'
  WRITE (*,'(10x,a)') '*                                         *'
  WRITE (*,'(10x,a)') '*   Write covar matrix as scaled eigen-   *'
  WRITE (*,'(10x,a)') '*    vectors and singular values into     *'
  WRITE (*,'(10x,a)') '*              NetCDF file                *'
  WRITE (*,'(10x,a/)') '*******************************************'

  ncfile_in = TRIM(inpath)//TRIM(infile)
  WRITE (*,*) 'Read trajectory from file: ',TRIM(ncfile_in)

  ncfile_out = TRIM(outpath)//TRIM(outfile)
  WRITE (*,*) 'Write covariance matrix to file: ',TRIM(ncfile_out)


! ************************************************
! *** Open trajectory file and read dimensions ***
! ************************************************

  s = 1
  stat(s) = NF_OPEN(TRIM(ncfile_in), NF_NOWRITE, ncid_in)
  s = s + 1

  ! Get dimensions
  stat(s) = NF_INQ_DIMID(ncid_in, 'dim_state', id_dim)
  s = s + 1
  stat(s) = NF_INQ_DIMLEN(ncid_in, id_dim, dim_state)
  s = s + 1
  stat(s) = NF_INQ_DIMID(ncid_in, 'timesteps', id_dim)
  s = s + 1
  stat(s) = NF_INQ_DIMLEN(ncid_in, id_dim, steps)

  ! Initialize time array
  ALLOCATE(times(steps))
  s = s + 1
  stat(s) = NF_INQ_VARID(ncid_in, 'time', id_time)
  s = s + 1
  stat(s) = NF_GET_VAR_DOUBLE(ncid_in, id_time, times)

  ! Get state variable ID
  s = s + 1
  stat(s) = NF_INQ_VARID(ncid_in, 'state', id_state)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading dimensions, no.', i
  END DO

  WRITE (*,'(/1x,a)') 'Dimensions of experiment:'
  WRITE (*,'(5x,1x,a,i10)') 'state dimension', dim_state
  WRITE (*,'(11x,a,i10)')   'time steps', steps

  IF (steps < maxtimes) THEN
     WRITE (*,'(1x,a)') '!! Number of available time slices is smaller than maxtimes - resetting!'
     maxtimes = steps
  END IF
  WRITE (*,'(13x,a8,i10)') 'maxtimes', maxtimes


! ****************************
! *** Read trajectory data ***
! ****************************

  WRITE (*,'(/1x,a)') '------- Read trajectory -------------'

  ALLOCATE(states(dim_state, maxtimes))
  ALLOCATE(meanstate(dim_state))

  read_in: DO iter = 1, maxtimes

     startv(2) = iter
     countv(2) = 1
     startv(1) = 1
     countv(1) = dim_state
     stat(1) = NF_GET_VARA_DOUBLE(ncid_in, id_state, startv, countv, &
          states(1 : dim_state, iter))
     IF (stat(1) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading state'

  END DO read_in

  ! Close trajectory file
  stat(1) = nf_close(ncid_in)
  IF (stat(1) /= NF_NOERR) &
       WRITE(*, *) 'NetCDF error in closing trajectory file'


! **********************************************
! *** Compute mean state and residual matrix ***
! **********************************************

  WRITE (*,'(/1x,a)') '------- Compute mean state and residual matrix -------------'

  ! *** gather mean state ***
  meanstate = 0.0
  DO i = 1, maxtimes
    meanstate(:) = meanstate(:) + states(:, i) / REAL(maxtimes)
  END DO

  ! get residual matrix
  DO i = 1, maxtimes
    states(:,i) = states(:,i) - meanstate(:)
  END DO


! *********************************************************
! *** Singular value decomposition of covariance matrix ***
! ***                                                   ***
! *** The covariance matrix is given by the state       ***
! *** sequences X of k states as                        ***
! ***          -1    _     _ T        T                 ***
! *** P = (k-1)   (X-X) (X-X)  = U L U  (EVP)           ***
! ***                                                   ***
! *** Thus we compute the singular value decomposition  ***
! ***     _        T            -1    2  T              ***
! ***   X-X = U S V ;  P = (k-1)   U S  U               ***
! ***                                                   ***
! ***                         -1/2                      ***
! *** and we store U and (k-1)     S in NetCDF.         ***
! *********************************************************

  WRITE (*,'(/1x,a)') '------- Compute SVD -------------'

  ALLOCATE(svals(maxtimes))
  ALLOCATE(svdU(dim_state, maxtimes))
  ALLOCATE(work(MAX(3 * MIN(dim_state, maxtimes) + &
       MAX(dim_state, maxtimes), 5 * MIN(dim_state, maxtimes))))
  ldwork = MAX(3 * MIN(dim_state, maxtimes) + &
       MAX(dim_state, maxtimes), 5 * MIN(dim_state, maxtimes))
    
  CALL dgesvd('s', 'n', dim_state, maxtimes, states, &
       dim_state, svals, svdU, dim_state, svdV, &
       dim_state, work, ldwork, dgesvd_info)
  
  ! *** rescale singular values ***
  DO i = 1, maxtimes
     svals(i) = svals(i) / SQRT(REAL(maxtimes - 1))
  END DO


! *********************************************************
! *** Write mean state and decomposed covariance matrix ***
! *********************************************************

! *** Determine rank to write ***

  getlimit: DO i = 1, maxtimes
     IF (svals(i) >= limit) THEN
        rank = i
     ELSE
        EXIT getlimit
     END IF
  END DO getlimit
  IF (rank < maxtimes) THEN
     WRITE (*,'(1x,a,i6,a,es10.2)') &
          'Use maximum of ', rank, ' eigenvectors due to eigenvalue-limit of ',limit
  END IF
  IF (rank == maxtimes) THEN
     rank = maxtimes - 1
     WRITE (*,'(5x,a,i4)') '++ reset rank to ',rank
  END IF

  WRITE (*,'(5x,a)') 'Singular values: '
  DO i = 1, rank
    WRITE (*, '(10x, i4, es12.3)') i, svals(i)
  END DO


  WRITE (*,'(/1x,a)') '------- Write decomposed covariance matrix and mean state -------------'

  ! *** Initialize file
  s = 1
  stat(s) = NF_CREATE(ncfile_out, 0, ncid_out) 

  attstr = 'Mean state, singular vectors and values of decomposed covariance matrix for FEOM'
  s = s + 1
  stat(s) = NF_PUT_ATT_TEXT(ncid_out, NF_GLOBAL, 'title', LEN_TRIM(attstr), &
       TRIM(attstr)) 

  ! Define dimensions
  s = s + 1
  stat(s) = NF_DEF_DIM(ncid_out, 'rank',  rank, dimid_rank)
  s = s + 1
  stat(s) = NF_DEF_DIM(ncid_out, 'dim_state', dim_state, dimid_state)
  s = s + 1
  stat(s) = NF_DEF_DIM(ncid_out, 'one',  1, dimid_one)

  ! Define variables
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'sigma', NF_DOUBLE, 1, dimid_rank, Id_sigma)

  ! mean state
  dimids(1) = DimId_state
  dimids(2) = dimid_one

  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'meanstate', NF_DOUBLE, 2, dimids, Id_mean)

  ! singular vectors
  dimids(1) = DimId_state
  dimids(2) = dimid_rank

  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'u_svd', NF_DOUBLE, 2, dimids, Id_svec)

  ! End Define mode
  s = s + 1
  stat(s) = NF_ENDDEF(ncid_out) 

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in init of output file, no.', i
  END DO

  ! Write singular values
  s = s + 1
  stat(s) = NF_PUT_VAR_DOUBLE(ncid_out, id_sigma, svals(1:rank))


  ! Write mean state
  startv(2) = 1
  countv(2) = 1
  startv(1) = 1
  countv(1) = dim_state
  s = s + 1
  stat(s) = NF_PUT_VARA_DOUBLE(ncid_out, id_mean, startv, countv, meanstate)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in writing state to output file, no.', i
  END DO


  ! *** Write singular vectors

  writevectors: DO i = 1, rank

    ! Write singular vector
     startv(2) = i
     countv(2) = 1
     startv(1) = 1
     countv(1) = dim_state
     s = 1
     stat(s) = NF_PUT_VARA_DOUBLE(ncid_out, id_svec, startv, countv, svdU(:,i))

     DO k = 1,  s
        IF (stat(k) /= NF_NOERR) &
             WRITE(*, *) 'NetCDF error in writing singular vectors, no.', i,' rank',i
     END DO

  END DO writevectors

  ! Close file
  s = 1
  stat(s) = NF_CLOSE(ncid_out)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in singular vectors to output file, no.', i
  END DO



! ********************
! *** Finishing up ***
! ********************

  DEALLOCATE(states, meanstate)
  DEALLOCATE(svals, svdU, work)

  WRITE (*,'(/1x,a/)') '------- END -------------'

END PROGRAM generate_covar

